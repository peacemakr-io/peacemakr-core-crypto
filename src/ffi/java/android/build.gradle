apply plugin: 'com.android.library'

task copySources(type: Copy) {
    from "${projectDir}/../java/src/main/java/io/peacemakr/corecrypto"
    into "${projectDir}/src/main/java/io/peacemakr/corecrypto"
    exclude "**/Crypto.java"
}

task copyTests(type: Copy) {
    from "${projectDir}/../java/src/test"
    into "${projectDir}/src/androidTest"
}

android {
    compileSdkVersion 30

   // Depending on what NDK you have locally, you may
   // need to update this.
   ndkVersion "21.3.6528147"

    defaultConfig {
        // Defines the minimum API level required to run the app.
        minSdkVersion 21

        // Specifies the API level used to test the app.
        targetSdkVersion 30

        // Defines the version number of your app.
        versionCode 1

        // Defines a user-friendly version name for your app.
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments "-DASAN=OFF", "-DSTATIC_LIBC=OFF", "-DPEACEMAKR_BUILD_ANDROID=ON",
                          "-DOPENSSL_ROOT=${projectDir}/../openssl/openssl-build",
                          "-DBUILD_TESTS=OFF", "-DANDROID_TOOLCHAIN=clang"
            }
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        /**
         * By default, Android Studio configures the release build type to enable code
         * shrinking, using minifyEnabled, and specifies the default Proguard rules file.
         */

        release {
            minifyEnabled true // Enables code shrinking for the release build type.
        }
    }

    externalNativeBuild {
        cmake {
            path "../../../../CMakeLists.txt"
        }
    }

    gradle.projectsEvaluated {
        preBuild.dependsOn(copySources)
        test.dependsOn(copyTests)
    }
}

dependencies {
    androidTestImplementation 'junit:junit:4.12'

    // Required for instrumented tests
    androidTestImplementation 'com.android.support:support-annotations:24.0.0'
    androidTestImplementation 'com.android.support.test:runner:0.5'
}
